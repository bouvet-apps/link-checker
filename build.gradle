plugins {
    id 'java'
    id 'com.enonic.xp.app' version '3.1.0'
    id "com.github.node-gradle.node" version "3.4.0"
    id 'org.ajoberstar.grgit' version '4.1.1'
    id "com.github.ben-manes.versions" version '0.42.0'
    id 'maven-publish'
}

sourceCompatibility = "1.11"
targetCompatibility = "1.11"

app {
    name = project.ext.appName
    displayName = project.ext.displayName
    vendorName = project.ext.vendorName
    vendorUrl = project.ext.vendorUrl
    systemVersion = "${xpVersion}"
}

sourceSets {
    main {
        resources {
            srcDir 'site'
        }
    }
}

task copyDist {
  dependsOn(':deploy')
  group 'application'
  description 'Copies jar file into dist folder'
  doLast {
    copy {
      from "build/libs/${projectName}-${version}.jar"
      into "dist/"
      rename ("${projectName}-${version}.jar", "${projectName}.jar")
    }
  }
}

task cleanDist(type: Delete) {
    delete "${rootDir}/dist/${projectName}.jar"
    println "Cleaned ${rootDir}/dist/${projectName}.jar"
}

task installWithCI(
        type: NpmTask,
        group: 'node',
        description: 'Install node dependencies with ci, ignoring scripts'
) {
    inputs.files "package.json"
    outputs.dir "./node_modules"
    args = ['ci', '--ignore-scripts', '--quiet']
}

task npmInstall(
        type: NpmTask,
        group: 'node',
        description: 'Install node dependencies, ignoring scripts'
) {
    args = ['install', '--ignore-scripts', '--quiet']
}

task npmBuild(type: NpmTask,
        group: 'node',
        dependsOn: 'installWithCI',
        description: 'Transpile ES6 files'
    ) {
    args = ['run', 'build']

    inputs.dir 'src/main/resources'
    outputs.dir "$buildDir/resources/main"
}

jar.dependsOn npmBuild
build.finalizedBy(copyDist)

clean.dependsOn(cleanDist)

dependencies {
    implementation "com.enonic.xp:core-api:${xpVersion}"
    implementation "com.enonic.xp:portal-api:${xpVersion}"
    include "com.enonic.xp:lib-auth:${xpVersion}"
    include "com.enonic.xp:lib-content:${xpVersion}"
    include "com.enonic.xp:lib-context:${xpVersion}"
    include "com.enonic.xp:lib-portal:${xpVersion}"
    include "com.enonic.xp:lib-websocket:${xpVersion}"
    include 'com.enonic.lib:lib-cache:2.1.1'
    include 'com.enonic.lib:lib-http-client:3.1.0'
    include "com.enonic.lib:lib-thymeleaf:2.0.1"
}

repositories {
    mavenLocal()
    mavenCentral()
    xp.enonicRepo()
    maven { url 'https://jitpack.io' }
    maven { url 'https://repo.enonic.com/public' }
}

node {
    // Version of node to use.
    version = '16.14.2'
    // Version of npm to use.
    npmVersion = '8.5.0'
    // If true, it will download node using above parameters.
    // If false, it will try to use globally installed node.
    download = true
}

wrapper {
    gradleVersion = '7.3.1'
    distributionType = Wrapper.DistributionType.BIN
}


publishing {
    publications {
        mavenJava( MavenPublication ) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/bouvet-apps/distro"
            credentials {
                username = project.property("bouvetGithubUser")
                password = project.property("bouvetGithubToken")
            }
        }
    }
}
