plugins {
    id 'java'
    id "com.moowork.node" version "1.3.1"
    id 'com.enonic.xp.app' version '2.0.0'
    id 'maven-publish'
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

app {
    name = project.ext.appName
    displayName = 'LinkChecker'
    vendorName = 'Bouvet Norge AS'
    vendorUrl = 'https://wwww.bouvet.no'
    systemVersion = "${xpVersion}"
}

sourceSets {
    main {
        resources {
            srcDir 'site'
        }
    }
}

task copyDist {
  dependsOn(':deploy')
  group 'application'
  description 'Copies jar file into dist folder'
  doLast {
    copy {
      from "build/libs/${projectName}-${version}.jar"
      into "dist/"
      rename ("${projectName}-${version}.jar", "${projectName}.jar")
    }
  }
}

task cleanDist(type: Delete) {
    delete "${rootDir}/dist/${projectName}.jar"
    println "Cleaned ${rootDir}/dist/${projectName}.jar"
}

task npmBuild(type: NpmTask,
        group: 'node',
        dependsOn: 'npmInstall',
        description: 'Transpile ES6 files'
    ) {
    args = ['run', 'build']

    inputs.dir 'src/main/resources'
    outputs.dir "$buildDir/resources/main"
}

jar.dependsOn npmBuild
build.finalizedBy(copyDist)

clean.dependsOn(cleanDist)

dependencies {
    compile "com.enonic.xp:core-api:${xpVersion}"
    compile "com.enonic.xp:portal-api:${xpVersion}"
    include "com.enonic.xp:lib-content:${xpVersion}"
    include "com.enonic.xp:lib-portal:${xpVersion}"
    include "com.enonic.xp:lib-auth:${xpVersion}"
    include "com.enonic.xp:lib-context:${xpVersion}"
    include "com.enonic.xp:lib-websocket:${xpVersion}"
    include "com.enonic.lib:lib-thymeleaf:2.0.0"
    include 'com.enonic.lib:lib-http-client:2.2.1'
    include 'com.enonic.lib:lib-cache:1.1.0'
}

repositories {
    mavenLocal()
    jcenter()
    xp.enonicRepo()
    maven { url 'https://jitpack.io' }
    maven { url 'http://repo.enonic.com/public' }
}

node {
    // Version of node to use.
    version = '12.16.0'
    // Version of npm to use.
    npmVersion = '6.13.4'
    download = true
}

wrapper {
    gradleVersion = '5.6.4'
    distributionType = Wrapper.DistributionType.BIN
}


publishing {
    publications {
        mavenJava( MavenPublication ) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/bouvet-apps/distro"
            credentials {
                username = project.property("bouvetGithubUser")
                password = project.property("bouvetGithubToken")
            }
        }
    }
}
